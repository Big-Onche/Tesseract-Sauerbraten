newui "scoreboard_multiteam" [
    if $mainmenu [hideui "scoreboard_multiteam"]
    uiallowinput 0
    UI_scoreboard_isteammode = (m_teammode (getmode))
    UI_scoreboard_isctfmode = (m_ctf (getmode))
    UI_scoreboard_getteams = [
        ? (UI_scoreboard_isteammode) (getteams) "good"
    ]
    UI_scoreboard_getteamplayers = [
        ? (UI_scoreboard_isteammode) (getteamplayers $arg1) (listclients 1)
    ]
    UI_scoreboard_getteamscore = [
        ? (UI_scoreboard_isteammode) (getteamscore $arg1) "Players"
    ]
    refreshscoreboard
    uicolor (? $sbtransparent $c_menu_a $c_menu) 0 0 [
        uivlist 0 [
            UI_sbheader
            uifill 0 0.005
            UI_multiteamscoreboard
            uispace 0 0.01
        ]
    ]
] [if $mainmenu [hideui "scoreboard_multiteam"]]

UI_multiteamscoreboard = [
    local get_player_stats get_player_context_color get_player_icon ispartner isclient header_width_mult
    header_width_mult = (*f (+f (! $hidefrags) $showping $showscore) 0.1)

    isclient = [= (getclientnum) $arg1]
    ispartner = [=s (getclientteam (getclientnum)) (getclientteam $arg1)]
    get_player_context_color = [
        ? (isauth $arg1) "^f5" (? (isadmin $arg1) "^f2" (? (ismaster $arg1) "^f0" (? (isclient $arg1) "^f8" (? (&& (m_teammode (getmode)) (ispartner $arg1)) "^f1" (? (m_teammode (getmode)) "^f3" "^f7")))))
    ]

    get_player_stats = [
        local player
        player = $arg1
        if (! $hidefrags) [ uitext (format "%3%1^f4/%4%2" (getclientfrags $player) (getclientdeaths $player) (? (isclient $player) "^f0" "^f4") (? (isclient $player) "^f3" "")) 0.4 ]
        if (&& $showscore (UI_scoreboard_isteammode)) [ uitext (format "^f9%1" (getclientflags $player)) 0.4 ]
        if $showping [ uitext (format "^f4%1" (scoreboardping $player)) 0.4 ]
    ]

    get_player_icon = [
        local model_index
        model_index = (? (isclient $arg1) $playermodel (getclientmodel $arg1))
        format "%1%2" (at ["mrfixit" "snoutx10k" "ogro" "inky" "captaincannon"] $model_index) (? (UI_scoreboard_isteammode) (? (ispartner $arg1) "_blue" "_red") "")
    ]

    uifill 0 0.15 [
        uivlist 0.005 [
            local grid_columns
            grid_columns = (max (div (listlen (UI_scoreboard_getteams)) (+ (>= (listlen (UI_scoreboard_getteams)) 3) 2)) 2)

            uigrid $grid_columns 0.006125 0.006125 [
                loop t (listlen (UI_scoreboard_getteams)) [
                    local team_name team_players
                    team_name = (at (UI_scoreboard_getteams) $t)
                    team_players = (listfilter cn (UI_scoreboard_getteamplayers $t) [! (isspectator $cn)])
                    if (listlen $team_players) [
                        uivlist 0 [
                            uifill $header_width_mult
                            uicolor (? (UI_scoreboard_isteammode) (? (=s (getclientteam (getclientnum)) $team_name) 0x4060D0 0xD04040) 0x40A040) (maxf $header_width_mult 0.2) 0 [
                                uitext (format "^f7%1" (UI_scoreboard_getteamscore $t)) 0.8
                                uiclamp 1 1 1 1
                            ]

                            uitable 0.04 0 [
                                uitableheader [
                                    uitext "^f4Name" 0.45
                                    if (! $hidefrags) [ uitext "^f4Frags/Deaths" 0.45 ]
                                    if (&& $showscore (UI_scoreboard_isteammode)) [ uitext "^f4Scores" 0.45 ]
                                    if $showping [ uitext "^f4Ping" 0.45 ]
                                ]
                                looplist player $team_players [
                                    uitablerow [
                                        uitextfill 9 0 [
                                            uihlist 0.001 [
                                                UIicon (get_player_icon $player) 0.02
                                                uialign -1
                                                uitext (format "%2%1" (getclientname $player) (get_player_context_color $player)) 0.4
                                                uitext (format "^f4(%1)" $player) 0.35
                                            ]
                                        ]

                                        get_player_stats $player
                                    ] [
                                        uicolor (? (isclient $player) 0x223355 (? (&& (UI_scoreboard_isteammode) (ispartner $player)) 0x111122 (? (UI_scoreboard_isteammode) 0x221111 0x111111))) (+f $header_width_mult 0.2) 0 [
                                            uioutline (? (isclient $player) 0xFFFFFF 0x222222) 0 0.02 [ 
                                                uiclamp 1 1 1 1
                                            ]
                                        ]
                                        if (isclient $player) [uispace 0 0.011]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                if (listlen (listfilter cn (listclients 1) [isspectator $cn])) [
                    uivlist 0.005 [
                        uicolor 0xbbbbbb $header_width_mult 0 [
                            uitext "Spectators" 0.8
                            uiclamp 1 1 1 1
                        ]
                        uitable 0.04 0 [
                            uitableheader [
                                uitext "^f4Name" 0.45
                                if (! $hidefrags) [ uitext "^f4Frags/Deaths" 0.45 ]
                                if (&& $showscore (UI_scoreboard_isteammode)) [ uitext "^f4Scores" 0.45 ]
                                if $showping [ uitext "^f4Ping" 0.45 ]
                            ]
                            looplist player (listclients 1) [
                                if (isspectator $player) [
                                    uitablerow [
                                        uitextfill 9 0 [
                                            uihlist 0.001 [
                                                UIicon (get_player_icon $player) 0.02
                                                uialign -1
                                                uitext (format "%2%1" (getclientname $player) (get_player_context_color $player)) 0.4
                                                uitext (format "^f4(%1)" $player) 0.35
                                            ]
                                        ]
                                        get_player_stats $player
                                    ] [
                                        uicolor (? (isclient $player) 0x223355 0x222222) (+f $header_width_mult 0.2) 0 [
                                            uioutline 0x333333 0 0.02 [ 
                                                uiclamp 1 1 1 1
                                            ]

                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]


        ]
    ]
]