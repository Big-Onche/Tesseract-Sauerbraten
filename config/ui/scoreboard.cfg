///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

scoreboard_padding = (*f 0.0075 $sbfontsize)

newui "scoreboard" [
    if $mainmenu [hideui "scoreboard"]
    uiallowinput 0
    UI_scoreboard_isteammode = (m_teammode (getmode))
    UI_scoreboard_isctfmode = (m_ctf (getmode))
    UI_scoreboard_getteams = [
        ? (UI_scoreboard_isteammode) (getteams) "good"
    ]
    UI_scoreboard_getteamplayers = [
        ? (UI_scoreboard_isteammode) (getteamplayers $arg1) (listclients 1)
    ]
    UI_scoreboard_getteamscore = [
        ? (UI_scoreboard_isteammode) (getteamscore $arg1) "Players"
    ]
    refreshscoreboard
    uicolor (? $sbtransparent $c_menu_a $c_menu) 0 0 [
        uihlist 0 [
            uispace $scoreboard_padding
            uivlist 0 [
                uispace 0 $scoreboard_padding
                UI_scoreboardheader
                uifill 0 0.005
                UI_multiteamscoreboard
                uispace 0 $scoreboard_padding
            ]
            uispace $scoreboard_padding
        ]
    ]

] [if $mainmenu [hideui "scoreboard"]]

UI_scoreboardheader = [
    uihlist 0.01 [
        uivlist 0 [
            uihlist 0.01 [
                title_font_size = (*f $sbfontsize 1.625)
                if $scoreboardservinfo [
                    UIicon (concatword (servicon)) (*f $sbfontsize 0.12)
                    uitext $scoreboardservinfo $title_font_size
                    if (&& $scoreboardmultiplayer $showip) [
                        uitext (concatword "^f4(^f1" $connectedip "^f4:^f1" $connectedport "^f4)") $title_font_size
                    ]
                ] [
                    UIicon "tesseract" (*f $sbfontsize 0.12)
                    uitext "^f7Tesseract: Sauerbraten" $title_font_size
                ]
            ]
            uihlist 0.015 [
                infos_font_size = (*f $sbfontsize 1.3)
                uihlist 0.003 [
                    uitext (format "%1%2%3" "^fs[" (concatword (at ["^f0" "^f0" "^f2" "^f3"] $getmastermode) (getmastermodename $getmastermode)) "^fS]") $infos_font_size
                ]
                UItriangle 0x606060 0.01 0.01 270
                uitext (getmodeprettyname $getmode) $infos_font_size
                UItriangle 0x606060 0.01 0.01 270
                uitext $scoreboardmap $infos_font_size
                if (m_timed $getmode) [
                    UItriangle 0x606060 0.01 0.01 270
                    uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) $infos_font_size
                ]
            ]
            uialign* -1
        ]
    ]
    uialign* -1
]

UI_multiteamscoreboard = [
    local get_player_stats get_player_context_color get_player_icon ispartner isclient header_width_mult normalized_font_size grid_height
    
    normalized_font_size = (*f $sbfontsize 2.5)
    header_width_mult = (*f (*f (+f (! $hidefrags) $showping $showscore) 0.1) (*f $normalized_font_size 1.3))
    grid_height = (*f $normalized_font_size 0.02)
    isclient = [= (getclientnum) $arg1]
    ispartner = [=s (getclientteam (getclientnum)) (getclientteam $arg1)]
    
    get_player_context_color = [
        ? (isauth $arg1) "^f5" (? (isadmin $arg1) "^f2" (? (ismaster $arg1) "^f0" (? (isclient $arg1) "^f8" (? (&& (m_teammode (getmode)) (ispartner $arg1)) "^f1" (? (m_teammode (getmode)) "^f3" "^f7")))))
    ]
    
    get_spectator_context_color = [
        ? (isauth $arg1) "^f5" (? (isadmin $arg1) "^f2" (? (ismaster $arg1) "^f0" "^f7"))
    ]
    
    get_ping_color = [
        ? (< $arg1 50) "^f0" (? (< $arg1 100) "^f2"  (? (< $arg1 150) "^f6" "^f3"))
    ]

    get_player_stats = [
        local player
        player = $arg1
        if (! $hidefrags) [ uitext (format "%3%1^f4/%4%2" (getclientfrags $player) (getclientdeaths $player) (? (isclient $player) "^f0" "^f4") (? (isclient $player) "^f3" "")) $sbfontsize ]
        if (&& $showscore (UI_scoreboard_isteammode)) [ uitext (format "^f9%1" (getclientflags $player)) $sbfontsize ]
        if $showping [ uitext (format "%2%1" (scoreboardping $player) (get_ping_color (scoreboardping $player))) $sbfontsize ]
    ]

    get_player_icon = [
        local model_index
        model_index = (? (isclient $arg1) $playermodel (getclientmodel $arg1))
        format "%1%2" (at ["mrfixit" "snoutx10k" "ogro" "inky" "captaincannon"] $model_index) (? (UI_scoreboard_isteammode) (? (ispartner $arg1) "_blue" "_red") "")
    ]
    
    get_spectators = [
        local spectators
        looplist player (listclients 1) [
            if (isspectator $player) [
                if (=s $spectators "") [
                    spectators = (concatword (getclientname $player))
                ] [
                    spectators = (concatword $spectators ", " (getclientname $player))
                ]
            ]
        ]  
        result $spectators
    ]

    uifill 0 0.15 [
        uivlist 0.005 [
            local grid_columns
            grid_columns = (max (div (listlen (UI_scoreboard_getteams)) (+ (>= (listlen (UI_scoreboard_getteams)) 3) 2)) 2)

            uigrid $grid_columns 0.006125 0.006125 [
                loop t (listlen (UI_scoreboard_getteams)) [
                    local team_name team_players
                    team_name = (at (UI_scoreboard_getteams) $t)
                    team_players = (listfilter cn (UI_scoreboard_getteamplayers $t) [! (isspectator $cn)])
                    if (listlen $team_players) [
                        uivlist 0 [
                            uifill $header_width_mult
                            uicolor (? (UI_scoreboard_isteammode) (? (=s (getclientteam (getclientnum)) $team_name) 0x884060D0 0x88D04040) 0x8840A040) (maxf $header_width_mult 0.2) 0 [
                                uitext (format "^f7%1" (UI_scoreboard_getteamscore $t)) (*f $sbfontsize 2)
                                uiclamp 1 1 1 1
                            ]

                            uitable 0.04 0 [
                                uitableheader [
                                    header_font_size = (*f $sbfontsize 1.12)
                                    uitext "^f4Name" $header_font_size
                                    if (! $hidefrags) [ uitext "^f4Frags/Deaths" $header_font_size ]
                                    if (&& $showscore (UI_scoreboard_isteammode)) [ uitext "^f4Score" $header_font_size ]
                                    if $showping [ uitext "^f4Ping" $header_font_size ]
                                ]
                                looplist player $team_players [
                                    uitablerow [
                                        uitextfill 12 0 [
                                            uihlist 0.001 [
                                                UIicon (get_player_icon $player) $grid_height
                                                uialign -1
                                                uitext (format "%2%1" (getclientname $player) (get_player_context_color $player)) $sbfontsize
                                                //uitext (format "^f4(%1)" $player) (*f $sbfontsize 0.88)
                                            ]
                                        ]
                                        get_player_stats $player
                                    ] [
                                        
                                        uicolor (? (isclient $player) 0x223355 (? (&& (UI_scoreboard_isteammode) (ispartner $player)) 0x111122 (? (UI_scoreboard_isteammode) 0x221111 0x111111))) (+f $header_width_mult 0.2) $grid_height [
                                            uioutline (? (isclient $player) 0xFFFFFF 0x222222) 0 $grid_height [ 
                                                uiclamp 1 1 1 1
                                            ]
                                        ]
                                        if (isclient $player) [uispace 0 0.011]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
    
    uispace 0 0.005
    
    if (listlen (listfilter cn (listclients 1) [isspectator $cn])) [
        uihlist 0.005 [
            UIicon "spectator" $grid_height
            uitext (format "%1%2%3^f7." "Spectators: " (get_spectator_context_color $player) (get_spectators)) $sbfontsize
        ]
    ]
    uialign* -1
]