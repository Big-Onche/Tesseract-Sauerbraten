///////////////////////////////////////////////////////////////////////////////
//  Player Setup                                                             //
///////////////////////////////////////////////////////////////////////////////
UI_pteam   = 0
UI_pweapon = 1
UI_PlayerIcon = ["mrfixit" "snoutx10k" "ogro" "inky" "captaincannon"]

UImenu "player_setup" [
    uihlist 0.01 [
        uivlist 0 [
            uiplayerpreview $playermodel 1 $UI_pteam $UI_pweapon 0.4 0.5 [
                style_generic_default
                uiclamp* 1 1 1 1
            ]
            uihlist 0 [
                UIlistslider UI_pweapon = [0 "Chain saw" 1 "Shotgun" 2 "Chain gun" 3 "Rocket launcher" 4 "Rifle" 5 "Grenade launcher" 6 "Pistol"] 0.4 $UI_optrowh
            ]
        ]
        uivlist 0.03 [
            uialign -2 -1
            uivlist 0.0037 [
                uitext "Name" 0.7
                uifield name 23 [] 0.712 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
                uialign* -1 -1
            ]
            uivlist 0.0037 [
                uitext "Player model" 0.7
                UIlistslider playermodel = [0 "Mr. Fixit" 1 "IronSnout X10K" 2 "Ogro" 3 "Inky" 4 "Captain Cannon"] 0.3587 $UI_optrowh
                uialign* -1 -1
            ]
            uivlist 0.0037 [
                uitext "Team" 0.7
                UIlistslider UI_pteam = [0 "^f0No team" 1 "^f1Good" 2 "^f3Evil"] 0.3587 $UI_optrowh
                uialign* -1 -1
            ]
            uivlist 0.0037 [
                uihlist 0.01 [uitext "Crosshair size" 0.7]
                uihlist 0 [
                    UIhslider crosshairsize 0 50 1 0.3 $UI_optrowh [uitext $crosshairsize 0.6]
                    uispace 0.0037
                    uifield crosshairsize 3 [] 0.65 style_generic_focus
                    uialign* -1 0
                ]
                
                uialign* -1 -1
            ]
            uivlist 0 [
                loop col (div (+ (listlen $crosshairs) 11) 12) [
                    uihlist 0.0037 [
                        loop row 12 [
                            if (&& (< (+ (* $col 12) $row) (listlen $crosshairs)) (= (mod (+ (* $col 8) $row) 2) 0)) [
                                uitarget 0.05 0.05 [
                                    local image_size
                                    uihover [image_size = 0.05] [image_size = 0.04]
                                    uiimage (concatword $crosshairs_dir (at $crosshairs (+ (* $col 12) $row))) $image_size $image_size
                                    uirelease [
                                        loadcrosshair (concatword $crosshairs_dir (at $crosshairs (+ (* $col 12) $row))) 0 
                                        loadcrosshair (concatword $crosshairs_dir (at $crosshairs (+ (* $col 12) (+ $row 1)))) 2
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                uialign* 0 0
            ]
        ]
    ]
] [] [] [] "Player Setup"