// This is an attempt to translate Sauer's GUI commands into something visually similar in Tesseract's UI.
// It's not 100% compatible, but it should work for basic interfaces.

newgui = [
    _gui_guilist_horizontal = 0
    _gui_guistrut_filler = []

    local gui_name gui_body gui_header
    gui_name = $arg1
    gui_body = $arg2
    gui_header = $arg3

    newui [gui_@gui_name] [
        uicolor 0x99333b40 0 0 [
            uispace 0.02 0.02 [
                uivlist 0 [
                    @@@@gui_body
                ]
            ]
        ]
    ]
]

showgui = [
    showui [gui_@arg1]
]

cleargui = [
    togglemainmenu
]

guitext = [
    uispace 0 0.003 [
        uialign -1 2
        uihlist 0 [
            if (|| (=s @@arg2 "") @@arg2) [
                uiimage "packages/interface/icons/info.jpg" 0.04 0.04
            ] [
                uispace 0 0.02 // small vertical spacer if no icon
            ]
            uitext [@@arg1] 0.8
            uialign* 1 0
        ]
    ]
]

guibutton = [
    uispace -0.008 0 [
        uialign -1 -2
        UIbutton "" [
            uihlist 0 [
                if (|| (=s @@arg3 "") @@arg3) [
                    uiimage "packages/interface/icons/menu.jpg" 0.04 0.04
                ]
                
                uitext [@@arg1] 0.8
            ]
        ] 0 0 [@@arg2]
    ]
]

guititle = [
    uihlist 0 [
        uitext [@@arg1] 0.9
    ]
]

guilist = [
    uialign -1 -1
    _gui_guilist_horizontal = (! $_gui_guilist_horizontal)
    if $_gui_guilist_horizontal [
        uihlist 0 [
            @arg1
            uialign -1 -1
        ]
    ] [
        uivlist 0 [
            @arg1
        ]
        uialign* -1 -1
    ]
    _gui_guilist_horizontal = (! $_gui_guilist_horizontal)
]

// dividing by 60 seems to give similar results to sauer's textbox
guitextbox = [
    uihlist 0 [
        uialign -1 -2
        uiwraptext [@@arg1] (divf @arg2 60) 0.8
    ]
]

guibar = [
    uifill 0 0.01 [UIbar 1 0; uiclamp 1 1 1 1]
]

// more magic numbers to match sauer's strut sizes
guistrut = [
    if $arg2 [
        uispace (divf $arg1 116) 0
    ] [
        uispace 0 (divf $arg1 43.9)
        arg2 = 0
    ]

    _gui_guistrut_filler = [@arg1 @arg2]

]

guispring = [
    local strut dir
    strut = (at $_gui_guistrut_filler 0)
    dir = (at $_gui_guistrut_filler 1)

    if $dir [
        uispace (divf $strut 116) 0
    ] [
        uispace 0 (divf $strut 43.9)
    ]
]

guifield = [
    uicolor 0x222222 0 0 [
        uifield $arg1 $arg2 $arg3 0.8 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
    ]
]

guiimage = [
    uiimage [@@arg1] @arg2 @arg3 [
        uiclamp 1 1 1 1
    ]
]
