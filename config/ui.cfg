// standard menu definitions
// do not modify, add personal menus to autoexec.cfg instead

exec "config/ui/lib.cfg"			// UI library
exec "config/ui/style.cfg"			// Styles
exec "config/ui/scoreboard.cfg"		// Scoreboard
exec "config/ui/edithud.cfg"		// Edit HUD
exec "config/ui/fkey.cfg"			// F#-Key Menus
exec "config/ui/serverbrowser.cfg"	// Server Browser
exec "config/ui/stats.cfg"			// Player Stats
exec "config/ui/options.cfg"		// Options Menu
exec "config/ui/gamesetup.cfg"		// Game setup Menu
exec "config/ui/playersetup.cfg"	// Player setup Menu

reui = [
    exec "config/ui.cfg"
]

togglemainmenu = [if $isconnected [|| hidetopui [toggleui main]] [if (! (uivisible "main")) [|| hidetopui [toggleui main]]]]

bind ESCAPE [togglemainmenu]

///////////////////////////////////////////////////////////////////////////////
//  Main                                                                     //
///////////////////////////////////////////////////////////////////////////////

// UI_MainMenuButton 1:(TEXT) 2:(ICON NAME) 3:[on-release]
UI_MainMenuButton = [
	UIbar 1
	UIbutton "hold1" [
		uivlist 0 [
			uihlist 0.01 [UIicon $arg2 0.04 ; uitext $arg1 0.7]
		]
	] 0.45 0.04 $arg3 -1
	UIbar 1
]

UImenu "main" [
    uieschide (! $mainmenu)
    uihlist 0 [
        uivlist 0 [

            uihlist 0 [
                UIbutton "hold1" [
                    uivlist 0 [
                        uihlist 0.01 [
                            UIicon (at $UI_PlayerIcon $playermodel) 0.1
                        ]
                    ]
                ] 0 0 [hideui "main" ; showui "player_setup"] -1
                uispace 0.005
                uivlist 0 [
                    uifield name 15 [] 1 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
                    uispace 0 0.01
                    uitext (at $UI_PlayerName $playermodel) 0.7
                    uialign* -1

                ]
            ]

            uifill 0.2 0.02
            UI_MainMenuButton "Server Browser" "arrow_fw" [hideui "main" ; showui "server_browser"]
            if $mainmenu [
                UI_MainMenuButton "Bot Match" "arrow_fw" [hideui "main" ; showui "bots"]
                UI_MainMenuButton "Campaign" "arrow_fw" [hideui "main" ; showui "campaign"]
            ]

            if $isconnected [
                uifill 0.2 0.02
                if (= $getmode 1) [ UI_MainMenuButton "Editing" "arrow_fw" [toggleui "env_settings"] ]
                UI_MainMenuButton "Vote Game Mode / Map" "arrow_fw" [showui "map_browser"]
                case $getteam [
                    0] [] [  
                    1] [UI_MainMenuButton "Switch Team: ^f3Evil" "action" [team "evil"]] [
                    2] [UI_MainMenuButton "Switch Team: ^f1Good" "action" [team "good"]]
                uifill 0.2 0.02 [UIbar 1]
                UI_MainMenuButton (concatword (? $isspectator "^f1Leave" "^f7Toggle") " Spectator") "spectator" [spectator (! (isspectator (getclientnum)))]
                UI_MainMenuButton "Master" "arrow_fw" [menu_master]
                if (ismaster $getclientnum) [
                    UI_MainMenuButton "Bots" "arrow_fw" [hideui "main" ; showui "bots"]
                ]
            ] [
                UI_MainMenuButton "Map Editor" "cube" [
                    hideui "main" ; mode 1 ; map ; edittoggle ; setselpos 512 512 512 ; outline 1
                    echo (concatword "^f7Editing keys:^n^t^f1" (searchbinds "edittoggle") "^f8: Switch between edit mode and test mode^n^t^f1" (searcheditbinds [toggleui "texture_browser"]) "^f8: Textures ^f7/ ^f1" (searcheditbinds "menu_entity") "^f8: Entities ^f7/ ^f1" (searcheditbinds [toggleui "mapmodel"]) "^f8: Objects ^f7/ ^f1" (searcheditbinds "menu_savemap") "^f8: Save map ^f7/ ^f1" (searcheditbinds [toggleui "env_settings"]) "^f8: Advanced editing menu")
                ]
            ]

            uifill 0.2 0.02
            UI_MainMenuButton "Options" "arrow_fw" [hideui "main" ; showui "options"]
            if $mainmenu [UI_MainMenuButton "About" "info" [hideui "main" ; showui "credits"]]
            UI_MainMenuButton (? $isconnected "Disconnect" "Quit") "exit" [if $isconnected [disconnect] [quit]]
        ]
        uivlist 0 [
        ]
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  Credits                                                                  //
///////////////////////////////////////////////////////////////////////////////
UI_creditlist = [
    UItitle [
        uispace 0.015 0 [
            uialign -1 -2
            uihlist 0.01 [
                uiimage "media/interface/cube.png" 0.03 0.03
                uitext [@@@@arg1] 0.7
            ]
        ]
    ] 0 0.032
    uispace 0.03 0 [
        uifill 0 0 [
            uiclamp 1 1
            uimodhgradient 0xC0C0C0 0xFFFFFF
            uiclamp- 1 1 1 1
            uispace 0.02 0.01 [
                uialign -1 -2
                uivlist 0 [
                    looplist i $arg2 [
                        uiwraptext (strreplace (strreplace $i "^"" "^fs^f6^"" "^"^fr") ": " "^f4: ") 0.69 0.6
                    ]
                    uialign* -1 -2
                ]
            ]
        ]
    ]
    uifill 0 0.02
]

UImenu "credits" [
    uihlist 0.01 [
        uiscroll 0.8 0.8 [
            uifill 0.77
            uivlist 0.0008 [
                uiclamp 1 1
                uialign -2 -1
                // Alphabetized by alias
                UI_creditlist "Code" [
                    [Lee "eihrul" Salzman]
                    [Benjamin Segovia]
                    [Kevin "Hirato Kirata" Meyer]
					[tesseract.gg]
                ]
                UI_creditlist "Additional Code" [
                    [Julian Mayer: MacOSX ports.]
					[Adrian "driAn" Henke: MD3 code.]
					[Jerry Siebe: Geometry rendering optimisations.]
                ]
                UI_creditlist "Level design" [
					[Kurt "kdoom" Kessler: A bunch of DM/capture maps, k_rpg1.]
					[Nieb: academy, authentic, autumn, bt_falls, c_valley, complex, curvy_castle, flagstone, garden, hallo, hashi, headroom, island, justice, nevil_c, nmp4, nmp8, nmp9, ot, park, ra, shipwreck, turbine]
					[John "metlslime" fitzgibbons: metl* maps.]
					[MitaMan: singleplayer episodes]
					[With additional maps by: Aardappel, driAn, Gilt, voot, Bryan "KillHour" Shalke, staffy, sparr, JCDPC, ZappaZ, RatBoy, Fanatic, rocknrol, KaiserTodretter, BlikjeBier, wurfel, aftasardem, Lazy [HUN], Gregor Koch, Junebug, Gabriele "Blindabuser" Magurno, PainKillAH, MeatROme, TUX, Mayhem, mIscreant, schmutzwurst, Kal, DairyKing, Hero, WahnFred, jonlimle, t-chen, viruz, Snowy, 4t3one, sinsch, Meister, Fatum, RaZgRiZ, SirAlex, Chris_0076, HeWho, Majikal, Suicizer, Ardelico, Cooper, Zeronic, Skur, flubz0r, SomeDude, Redon, MisanthropiX, Doko, Destroyer, TomekGNO, Kretren, Drakker, KI113R, rabe, SuperMan, Crap_I'm_Dead, b4lkLu, Windecker, Justin, and others.]
                ]
                UI_creditlist "Art / Content" [
					[John "Geartrooper" Siar: Mr. Fixit, Ironsnout, RPG characters, monsters, new hudguns and vweps.]
					[Gabriele "Blindabuser" Magurno: Logos, loading screen, announcer voices.]
					[MakkE: Mapmodels, old hudguns, items.]
					[Dietmar "dcp" Pier: Mapmodels, old hudguns.]
					[DarthVim: Old hudguns.]
					[Nieb: Textures, Mapmodels, Skyboxes.]
					[Sock: The egyptian & tech texture sets (website).]
					[Iikka "Fingers" Keranen: The ikbase ik2k texture sets (website).]
					[Lunaran, Gibbie, Gregor Koch, Jésus "aftasardem" Maia, MitaMan, and philipk: Normalmapped texture sets.]
					[SkiingPenguins: Skyboxes.]
					[Additional art by: metlslime (textures), Than (textures), Remedy Entertainment Ltd (textures), Seth & Ulrich Galbraith (GPL models), Brian "EvilBastard" Collins, Conrad, Magarnigal, Psionic, James Green, Andreas Möller, Ryan Butts & Jeramy Cooke (md2 models), KaiserTodretter (items), Tentus (mapmodels), Kurt Kessler (mapmodels), Philip Klevestav (textures), leileilol/OpenArena (GPL bullet hole decal).]
                ]
                UI_creditlist "Sound / Music" [
					[Marc "Fanatic" A. Pullen: Soundtrack.]
					[Dylan "Dacker" Ackerly]
                    [Shane "Nieb" Nieb]
                    [Sławomir "Q009" Błauciak]
                    [Jannic "Uphys" Böhme]
                ]
                UI_creditlist "Miscellaneous" [
                    [Kevin "Hirato Kirata" Meyer : CubeScript Documentation]
                    [Shane "Nieb" Nieb : UI Design, Tesseract Website]
                    [Daniel "q66" Kolesa : Build-system Improvements]
                    ["RaZgRiZ" : UI Documentation, UI Design]
                ]
                UI_creditlist "Cube Engine 2" [
                    [Wouter "Aardappel" van Oortmerssen]
                    [Lee "eihrul" Salzman]
                    [Mike "Gilt" Dysart]
                    [Robert "baby-rabbit" Pointon]
                    [Quinton "Quin" Reeves]
                    [John "geartrooper" Siar]
                    [and others...]
                    [sauerbraten.org]
                ]
                UI_creditlist "Sauerract port" [
                    [Jean "BIG-Onche" Onche]
                    ["Salatiel" S.]
                ]
                uiclamp* 1 1
            ]
        ]
        UIvscroll 0.02 0.8 2.0
    ]
] [] [] [] "^"Tesseract: Sauerbraten^" Contributors"

///////////////////////////////////////////////////////////////////////////////
//  Master                                                                   //
///////////////////////////////////////////////////////////////////////////////
menu_master = [
    if $isconnected [toggleui "master"]
]

UImenu "master" [
    uifill 0 0 [
        uivlist 0 [
            uihlist 0.01 [
                if (ismaster $getclientnum) [
                    UIbutton "" [uitext "Relinquish Master" 0.65] 0.24 0.06 [setmaster 0]
                ] [
                    if (issomeoneelsemaster) [
                        UIbutton "" [uitext "^f4Claim Master" 0.65] 0.24 0.06 []
                    ] [
                        UIbutton "" [uitext "Claim Master" 0.65] 0.24 0.06 [setmaster 1]
                    ]
                ]
                if (ismaster $getclientnum) [
                    uigrid 2 0 0 [
                        looplist2 n i [
                            "^f0open ^f4(0)"   0 "^f0veto ^f4(1)"    1
                            "^f2locked ^f4(2)" 2 "^f3private ^f4(3)" 3
                        ] [
                            UIbutton "" [
                                UIradio [= $getmastermode $i] 0.016
                                uitext $n 0.6
                            ] 0.16 0.03 [mastermode $i] -1
                        ]
                    ]
                    uivlist 0 [
                        UIbutton "" [uitext "Clearbans" 0.65] 0.16 0.03 [clearbans]
                        UIbutton "" [uitext "Bots" 0.65] 0.16 0.03 [hideui "main" ; showui "bots"]
                    ]
                ] [
                    uifill 0.49
                ]
            ]
            uifill 0 0.02 [UIbar 1 ; uiclamp 1 1 1 1]
            if (ismaster $getclientnum) [
                if (> $getmode 2) [UI_clientlistteam] [UI_clientlistsolo]
            ] [
                if (> $getmode 2) [uifill 1.43 0.312] [uifill 0 0.624]
            ]
        ]
    ]
] [] [] [] "Master"

UI_clientlistsolo = [
    uihlist 0 [
        uiscroll 0.71 0.624 [
            uifill 0.71 0.624 [
                uitable 0 0 [
                    UI_mlc = 0
                    looplist cn (listclients 1 0) [
                        uitablerow [
                            uifill 0.078 0.026 [
                                uicolortext (concatword $cn " : ") (scoreboardstatus $cn) 0.57
                                uialign- 1
                            ]
                            uifill 0.362 0.026 [
                                uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
                                uialign- -1
                            ]
                            if (= $cn $getclientnum)[
                                uifill 0.037 0.026
                            ] [
                                UIbutton "hold2" [
                                    UItriangle 0xCCCCCC 0.01 0.01 270
                                    uitext "Kick" 0.57
                                ] 0.077 0.026 [kick @cn] -1
                            ]
                            UIbutton "hold2" [
                                UIradio [= (isspectator $cn) 1] 0.016
                                uitext "Spec" 0.57
                            ] 0.087 0.026 [spectator @(= (isspectator $cn) 0) @cn] -1
                            UIbutton "hold2" [
                                UIradio [= (ismaster $cn) 1] 0.016
                                uitext (concatword (? (isadmin $cn) "Admin" (? (isauth $cn) "Auth" "Master"))) 0.57
                            ] 0.105 0.026 [setmaster (? (ismaster $cn) 0 1) @cn] -1
                        ] [
                            UI_mlc = (! $UI_mlc)
                            uicolor (? $UI_mlc 0x99323232 0x99262626) 0 0 [
                                uiclamp 1 1 1 1
                                pushif highlight (scoreboardhighlight $cn) [
                                    uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                                ]
                            ]
                        ]
                    ]
                ]
                uialign* -1 -1
            ]
        ]
        uifill 0.01
        UIvscroll 0.02 0.624 1.56
    ]
]

UI_clientlistteam = [
    uihlist 0 [
        uiscroll 1.4 0.312 [
            uifill 1.4 0.312 [
                uihlist 0 [
                    uialign 0 -1
                    uitable 0 0 [
                        uifill 0.695
                        UI_mlc = 0
                        looplist cn (listclients 1 0) [
                            if (= (getclientteam $cn) 1) [
                                uitablerow [
                                    uifill 0.078 0.026 [
                                        uicolortext (concatword $cn " : ") (scoreboardstatus $cn) 0.57
                                        uialign- 1
                                    ]
                                    uifill 0.26 0.026 [
                                        uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
                                        uialign- -1
                                    ]
                                    if (= $cn $getclientnum)[
                                        uifill 0.077 0.026
                                    ] [
                                        UIbutton "hold2" [
                                            UItriangle 0xCCCCCC 0.01 0.01 270
                                            uitext "Kick" 0.57
                                        ] 0.077 0.026 [kick @cn] -1
                                    ]
                                    UIbutton "hold2" [
                                        UItriangle 0xCCCCCC 0.01 0.01 270
                                        uitext "Team" 0.57
                                    ] 0.0875 0.026 [setteam @cn @(? (=s (getclientteam $cn) "1") "2" "1")] -1
                                    UIbutton "hold2" [
                                        UIradio [= (isspectator $cn) 1] 0.016
                                        uitext "Spec" 0.57
                                    ] 0.087 0.026 [spectator @(= (isspectator $cn) 0) @cn] -1
                                    UIbutton "hold2" [
                                        UIradio [= (ismaster $cn) 1] 0.016
                                        uitext (concatword (? (isadmin $cn) "Admin" (? (isauth $cn) "Auth" "Master"))) 0.57
                                    ] 0.105 0.026 [setmaster (? (ismaster $cn) 0 1) @cn] -1
                                ] [
                                    UI_mlc = (! $UI_mlc)
                                    uicolor (? $UI_mlc 0x99333b40 0x99262b33) 0 0 [
                                        uiclamp 1 1 1 1
                                        pushif highlight (scoreboardhighlight $cn) [
                                            uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uifill 0.01
                    uitable 0 0 [
                        uifill 0.695
                        UI_mlc = 0
                        looplist cn (listclients 1 0) [
                            if (= (getclientteam $cn) 2) [
                                uitablerow [
                                    uifill 0.078 0.026 [
                                        uicolortext (concatword $cn " : ") (scoreboardstatus $cn) 0.57
                                        uialign- 1
                                    ]
                                    uifill 0.26 0.026 [
                                        uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
                                        uialign- -1
                                    ]
                                    if (= $cn $getclientnum)[
                                        uifill 0.077 0.026
                                    ] [
                                        UIbutton "hold2" [
                                            UItriangle 0 0.01 0.01 270
                                            uitext "Kick" 0.57
                                        ] 0.077 0.026 [kick @cn] -1
                                    ]
                                    UIbutton "hold2" [
                                        UItriangle 0 0.01 0.01 270
                                        uitext "Team" 0.57
                                    ] 0.0875 0.026 [setteam @cn @(? (=s (getclientteam $cn) "1") "2" "1")] -1
                                    UIbutton "hold2" [
                                        UIradio [= (isspectator $cn) 1] 0.016
                                        uitext "Spec" 0.57
                                    ] 0.087 0.026 [spectator @(= (isspectator $cn) 0) @cn] -1
                                    UIbutton "hold2" [
                                        UIradio [= (ismaster $cn) 1] 0.016
                                        uitext (concatword (? (isadmin $cn) "Admin" (? (isauth $cn) "Auth" "Master"))) 0.57
                                    ] 0.105 0.026 [setmaster (? (ismaster $cn) 0 1) @cn] -1
                                ] [
                                    UI_mlc = (! $UI_mlc)
                                    uicolor (? $UI_mlc 0x99403333 0x99332626) 0 0 [
                                        uiclamp 1 1 1 1
                                        pushif highlight (scoreboardhighlight $cn) [
                                            uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uialign* 0 -1
                ]
            ]
        ]
        uifill 0.01
        UIvscroll 0.02 0.312 1.56
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  Bots                                                                     //
///////////////////////////////////////////////////////////////////////////////
UI_botskill = 87
UI_botaddnum = 9
UI_botminskill = 50
UI_botmaxskill = 100

startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $UI_botaddnum [addbot (rnd (+ $UI_botmaxskill 1) $UI_botminskill )]
    ]
]

UImenu "bots" [
    uifill 0.65 0.2 [
        uivlist 0.01 [
            if (isconnected) [
                uitext (concatword "Bots: " (numbots)) 0.6
                uifill 0 0.01
            ]

            uihlist 0.007 [
                uialign 1
                uitext "^f0Lazy" 0.6
                uispace 0.14
                uitext "^f6Expert" 0.6
            ]
            UI_botminskill = (min (max (min $UI_botmaxskill $UI_botminskill) 1) 110)
            UI_botmaxskill = (max (min (max $UI_botmaxskill $UI_botminskill) 110) 1)
            uihlist 0.01 [
                uivlist 0.015 [
                    uihlist 0 [ uialign -1 ; uitext "Bot Skill" 0.7 ]
                    uihlist 0 [ uialign -1 ; uitext "Bot Minimum Skill" 0.7]
                    uihlist 0 [ uialign -1 ; uitext "Bot Maximum Skill" 0.7]
                    uihlist 0 [ uialign -1 ; uitext "Bot Amount" 0.7]
                ]
                
                uivlist 0.01 [
                    UIhslider UI_botskill 1 110 1 0.4 0.03 [uitext $UI_botskill 0.7] [UI_botminskill = $UI_botskill ; UI_botmaxskill = $UI_botskill ]
                    UIhslider UI_botminskill 1 $UI_botmaxskill 1 0.4 0.03 [uitext (concatword $UI_botminskill "^f4-" $UI_botmaxskill) 0.7] [UI_botskill = (format "^f4Random (%1-%2)" $UI_botminskill $UI_botmaxskill)]
                    UIhslider UI_botmaxskill (? (= $UI_botminskill $UI_botmaxskill) (- $UI_botminskill 1) $UI_botminskill) 110 1 0.4 0.03 [uitext (concatword "^f4" $UI_botminskill "-^f7" $UI_botmaxskill) 0.7] [UI_botskill = (format "^f4Random (%1-%2)" $UI_botminskill $UI_botmaxskill)]
                    UIhslider UI_botaddnum 2 32 1 0.4 0.03 [uitext $UI_botaddnum 0.7]
                ]
            ]

            if (isconnected) [
                uifill 0 0.015
                uihlist 0.05 [
                    uivlist 0.01 [
                        UIbutton "" [uitext "Add Bot" 0.65 ; UI_keyref 1 0.027 0.027 "Ins" "" 0.4] 0.2 0.04 [addbot (rnd (+ $UI_botmaxskill 1) $UI_botminskill)]
                        UIbutton "" [uitext (concatword "Add " $UI_botaddnum " Bots") 0.65] 0.2 0.04 [loop a (UI_botaddnum) [addbot (rnd (+ $UI_botmaxskill 1) $UI_botminskill)]]
                    ]
                    uivlist 0.01 [
                        UIbutton "" [uitext "Delete Bot" 0.65 ; UI_keyref 1 0.027 0.027 "Del" "" 0.4] 0.2 0.04 [delbot]
                        UIbutton "" [uitext "Clear Bots" 0.65] 0.2 0.04 [clearbots]
                    ]
                ]
            ] [
                UIbutton "" [uitext "^f8Start Bot Match" 0.6] 0.18 0.042 [
                    UI_onmapselect = [ startbotmatch ]
                    hideui "bots"
                    showui "map_browser"
                ]
                //UI_MainMenuButton "Start match" "arrow_fw" [hidetopui; showui "campaign"]
            ]
        ]
    ]
] [ UI_onmapselect = [] ] [] [] "Bots"

///////////////////////////////////////////////////////////////////////////////
//  Display Changes                                                          //
///////////////////////////////////////////////////////////////////////////////
UImenu "changes" [
    uivlist 0.02 [
        uiclamp 1 1
        UItitle [uitext "Apply changes for:" 0.6] 0 0.03
        uiclamp- 1 1
        uivlist 0.01 [
            loop i $pendingchanges [
                uitext (concatword "^f8" (pendingchanges $i)) 0.6
            ]
        ]
        uihlist 0 [
            UIbutton "hold2" [uitext "Yes" 0.6] 0.18 0.032 [applychanges]
            UIbutton "hold2" [uitext "No"  0.6] 0.18 0.032 [hideui "changes"]
        ]
    ]
] [if (= $pendingchanges 0) [hideui "changes"]]


///////////////////////////////////////////////////////////////////////////////
//  Notepad                                                                  //
///////////////////////////////////////////////////////////////////////////////
UI_notepadfile = "untitled.txt"
UI_cmc_from   = ""
UI_cmc_to     = ""

UImenu "notepad" [
    uivlist 0.01 [
        uihlist 0.01 [
            uialign -1 -2
            uifield UI_notepadfile -35 [] 0.6 [
                uioutline (uifocus? 0xFF0000 $c_line)
                uiclamp- 1 1 1 1
            ]
            UIbutton "hold2" [uitext "Load"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textload $UI_notepadfile]
            UIbutton "hold2" [uitext "Save"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textsave $UI_notepadfile]
            UIbutton "hold2" [uitext "Exec"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textexec]
            uispace 0.01 0 [UIbar 0 1]
            UIbutton "hold2" [uitext "Copy"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textcopy]
            UIbutton "hold2" [uitext "Paste"      0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textpaste]
            UIbutton "hold2" [uitext "Select All" 0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textselectall]
            uispace 0.01 0 [UIbar 0 1]
            UIbutton "hold2" [uitext "Clear All"  0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textclear]
        ]
        uitexteditor $UI_notepadfile -120 30 0.6 "" 0 [
            uioutline (uifocus? 0xFF0000 $c_line)
            uiclamp- 1 1 1 1
        ]
        textinit $UI_notepadfile $UI_notepadfile
    ]
] [] [] [] "Notepad"

notepad = [
    if $numargs [UI_notepadfile = $arg1]
    showui "notepad"
]

mapnotepad = [
    UI_notepadfile = (concatword "media/map/" (? $numargs $arg1 $mapname) ".cfg")
    showui "notepad"
]

copymapcfg = [
    if $numargs [
        UI_cmc_from = $arg1
        UI_cmc_to   = (? (= $numargs 2) $arg2 $mapname)
        if (findfile (concatword "media/map/" $UI_cmc_from ".cfg")) [
            if (findfile (concatword "media/map/" $UI_cmc_to ".cfg")) [showui "cmc_confirm"] [
                textfocus "#copymapcfg"
                textload (concatword "media/map/" $UI_cmc_from ".cfg")
                textsave (concatword "media/map/" $UI_cmc_to   ".cfg")
            ]
        ] [
            echo (concatword "^f7Map config "^f8^" $UI_cmc_from "^f7^" does not exist!")
            UI_cmc_from = "" ; UI_cmc_to = ""
        ]
    ]
]

UImenu "cmc_confirm" [
    uivlist 0.02 [
        uiclamp 1 1
        UItitle [uitext "Copy map config:" 0.6] 0 0.03
        uiclamp- 1 1
        uivlist 0.01 [
            uitext (concatword "^f2" $UI_cmc_from) 0.6
            UItriangle 0 0.01 0.01 180
            uitext (concatword "^f2" $UI_cmc_to) 0.6
        ]
        uihlist 0 [
            UIbutton "hold2" [uitext "Proceed" 0.6] 0.18 0.032 [
                textfocus "#copymapcfg"
                textload (concatword "media/map/" $UI_cmc_from ".cfg")
                textsave (concatword "media/map/" $UI_cmc_to   ".cfg")
                hideui "cmc_confirm"
            ]
            UIbutton "hold2" [uitext "Cancel"  0.6] 0.18 0.032 [hideui "cmc_confirm"]
        ]
    ]
] [
    if (|| [=s $UI_cmc_from ""] [=s $UI_cmc_to ""]) [hideui "cmc_confirm"]
] [UI_cmc_from = "" ; UI_cmc_to = ""]



///////////////////////////////////////////////////////////////////////////////
//  SP Campaign                                                              //
///////////////////////////////////////////////////////////////////////////////
// - each sub-array represents a campaign.
// - the first element is the campaign title.
// - subsequent elements are [mapname, title] pairs for campaigns with multiple "chapters".
// - campaigns without chapters contain a single [mapname, title] pair.
_sp_campaign_maps_markup = [
    [
        "^f8Private Stan Sauer"
        ["mpsp9a" "^f0Run N' Gun Part I"       ]
        ["mpsp9b" "^f2Run N' Gun Part II"      ]
        ["mpsp9c" "^f6Run N' Gun Part III"     ]
        ["mpsp10" "^f3THE SERIOUSLY BIG VALLEY"]
    ]
    [
        "^f8An Army Of One"
        ["mpsp6a" "^f0Part I"  ]
        ["mpsp6b" "^f6Part II" ]
        ["mpsp6c" "^f3Part III"]
    ]
    [ [ "lost" "^f5Lost"             ] ]
    [ [ "skrsp1" "^f5Meltdown"       ] ]
    [ [ "crnsp1" "^f5Missile Pass"   ] ]
    [ [ "level9" "^f5Level 9"        ] ]
]

looplist sp_campaign $_sp_campaign_maps_markup [
    if (> (listlen $sp_campaign) 1) [
        local sp_chapters sp_chapters_buttons
        sp_chapters = (concatword "[" (sublist $sp_campaign 1) "]")
        UImenu (escape (at $sp_campaign 0)) [
            uivlist 0.005 [
                uitext "^f2Single Player Modes" 0.7
                uitext (concat @@(at $sp_campaign 0)) 0.7
                uitext "^f4Select a Chapter to play" 0.5
                looplist sp_chapter @@sp_chapters [
                    local bestscore
                    bestscore = [bestscore_@(at $sp_chapter 0)]
                    
                    UI_MainMenuButton (concat (at $sp_chapter 1)) (? (getalias $bestscore) "checkbox_on" "checkbox_off") [ sp @(at $sp_chapter 0) ]

                    if (getalias $bestscore) [
                        uitext (format "^f5[Completed] Map: %1 / Best score: %2" @(at $sp_chapter 0) $$bestscore) 0.4
                    ]
                ]
                UI_MainMenuButton "back" "arrow_bw" [hidetopui; showui "campaign"]
            ]
        ]
    ]
]

UImenu "campaign" [
    uivlist 0.005 [
        uitext "^f2Single Player Modes" 0.7
        looplist sp_campaign $_sp_campaign_maps_markup [
            if (> (listlen $sp_campaign) 1) [
                UI_MainMenuButton (concat "Open" (at $sp_campaign 0)) "arrow_fw" [hideui "campaign" ; showui (escape @(at $sp_campaign 0))]
            ] [
                UI_MainMenuButton (concat "Play" (at $sp_campaign 0 1)) "cube" [hideui "campaign" ; sp @(at $sp_campaign 0)]
            ]
        ]
        UI_MainMenuButton "back" "arrow_bw" [hidetopui]
    ]
]
